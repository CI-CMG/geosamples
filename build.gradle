buildscript {
	ext {
		springBootVersion = '2.0.0.RELEASE'
	}
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:1.0.9"
	}
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.ewerk.gradle.plugins.querydsl"

group = 'cmg.geosamples'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven {
		url 'https://repo.spring.io/libs-release'
	}
	// Use local artifactory for DB drivers when run in ncei network,
	// run with '-Pncei' flag. If not in ncei network, be sure you have an
	// ojdbc (oracle db connecting) jar locally in 'libs' folder
	if (project.hasProperty('ncei')) {
		maven {
			url 'https://devtools.ngdc.noaa.gov/artifactory/contrib-release-local'
		}
	}
}


dependencies {
	compile('org.springframework.boot:spring-boot-starter-actuator')
	compile('org.springframework.boot:spring-boot-starter-data-jpa')
	compile('org.springframework.boot:spring-boot-starter-jdbc')
	compile('org.springframework.boot:spring-boot-starter-web')
	if (project.hasProperty('ncei')) {
		compile "com.oracle:ojdbc6:11.2.0.3"
	} else {
		compile fileTree(dir: 'libs', include: '*.jar') /* Primarily for local ojdbc.  */
	}

	compile "com.querydsl:querydsl-jpa:${querydsl_version}"
	compileOnly "com.querydsl:querydsl-apt:${querydsl_version}"

	runtime('org.springframework.boot:spring-boot-devtools')
	compile('org.projectlombok:lombok')
	compile 'com.vividsolutions:jts:1.13'
	compile 'org.springframework.data:spring-data-rest-webmvc:3.0.5.RELEASE'
  compile "org.hibernate:hibernate-spatial:5.2.14.Final"
	testCompile('org.springframework.boot:spring-boot-starter-test')
}

querydsl {
	jpa = true
	querydslSourcesDir = "$buildDir/generated/source/apt/main"
}

sourceSets {
  main {
    java {
      srcDir "$buildDir/generated/source/apt/main"
    }
  }
}
